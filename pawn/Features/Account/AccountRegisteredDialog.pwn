// Copyright 2006-2015 Las Venturas Playground. All rights reserved.
// Use of this source code is governed by the GPLv2 license, a copy of which can
// be found in the LICENSE file.

/**
 * When a player joins Las Venturas Playground with a nickname that has been registered by someone
 * else, we'll have to show them a dialog explaining the situation. In this dialog there are two
 * options, the [Ok] button which allows them to enter their password again, or the [Cancel] button
 * which will change their nickname in something generated by the GuestNameGenerator.
 *
 * @author Russell Krupke <russell@sa-mp.nl>
 */
class AccountRegisteredDialog <playerId (MAX_PLAYERS)> {
    // What is the dialog Id that will be used for the account registered informational dialogs?
    public const DialogId = @counter(OnDialogResponse);

    /**
     * Display the account registered dialog for a certain player. After the player commits an
     * action, the AccountRegisteredDialog::onResponse method will automatically be invoked.
     */
    public show() {
        ShowPlayerDialog(playerId,
                         AccountRegisteredDialog::DialogId,
                         DIALOG_STYLE_MSGBOX,
                         "Your nickname has been registered!",
                         "The nickname you're using has been registered on {FF0000}www.sa-mp.nl{FFFFFF}.\n" ...
                            "If you have not registered the account, click on \"{40FF40}Play{FFFFFF}\" to start playing\n" ...
                            "as a guest. Otherwise, click on \"{40FF40}Identify{FFFFFF}\" to try logging in again.",
                         "Play",
                         "Identify");
    }

    /**
     * After the player has pressed either "Ok" or "Cancel" in the information dialog, we'll need
     * to handle their action and continue with the login flow appropriately.
     *
     * @param button The button which was clicked, see the DialogButton enumeration.
     * @param listItem Index of the selected item in the list, which starts at zero.
     * @param inputText The text which was inserted, if any.
     * @return integer Were we able to correctly handle this callback?
     */
    @switch(OnDialogResponse, AccountRegisteredDialog::DialogId)
    public onResponse(DialogButton: button, listItem, inputText[]) {
        if (button == LeftButton) { // "Play"
            Account(playerId)->changeNicknameAndPlayAsGuest();
            return 1;
        }

        Account(playerId)->displayPasswordDialog();

        #pragma unused listItem, inputText
        return 1;
    }
};
